# Maintainer: Remy Marquis <remy.marquis at gmail dot com>

pkgname=pacaur
pkgver=4.1.30
pkgrel=1
pkgdesc="A fast workflow AUR helper using cower as backend"
arch=('any')
url="https://github.com/Spyhawk/pacaur"
license=('GPL')
depends=('cower' 'expac' 'sudo' 'pacman')
makedepends=('perl')
backup=('etc/xdg/pacaur/config')
source=("https://github.com/Spyhawk/$pkgname/archive/$pkgver.tar.gz")
md5sums=('ff447c38b2f2cb997656ae7c2a5685e5')

build() {
    cd "$pkgname-$pkgver"
    pod2man --utf8 --section=8 --center="Pacaur Manual" --name="PACAUR"\
        --release="$pkgname $pkgver" ./README.pod > ./pacaur.8
}

package() {
    cd "$pkgname-$pkgver"

    mkdir -p $pkgdir/etc/xdg/pacaur
    install -D -m644 ./config $pkgdir/etc/xdg/pacaur/config
    mkdir -p $pkgdir/usr/bin
    install -D -m755 ./$pkgname $pkgdir/usr/bin/$pkgname
    mkdir -p $pkgdir/usr/share/bash-completion/completions
    install -D -m644 ./bash.completion\
        $pkgdir/usr/share/bash-completion/completions/$pkgname
    mkdir -p $pkgdir/usr/share/zsh/site-functions
    install -D -m644 ./zsh.completion\
        $pkgdir/usr/share/zsh/site-functions/_pacaur
    mkdir -p $pkgdir/usr/share/man/man8
    install -D -m644 ./pacaur.8 $pkgdir/usr/share/man/man8/pacaur.8
    mkdir -p $pkgdir/usr/share/locale/{de,es,fr,it,ja,pl,ru}/LC_MESSAGES/
    msgfmt ./po/de.po -o $pkgdir/usr/share/locale/de/LC_MESSAGES/pacaur.mo
    msgfmt ./po/es.po -o $pkgdir/usr/share/locale/es/LC_MESSAGES/pacaur.mo
    msgfmt ./po/fr.po -o $pkgdir/usr/share/locale/fr/LC_MESSAGES/pacaur.mo
    msgfmt ./po/it.po -o $pkgdir/usr/share/locale/it/LC_MESSAGES/pacaur.mo
    msgfmt ./po/ja.po -o $pkgdir/usr/share/locale/ja/LC_MESSAGES/pacaur.mo
    msgfmt ./po/pl.po -o $pkgdir/usr/share/locale/pl/LC_MESSAGES/pacaur.mo
    msgfmt ./po/ru.po -o $pkgdir/usr/share/locale/ru/LC_MESSAGES/pacaur.mo
}

